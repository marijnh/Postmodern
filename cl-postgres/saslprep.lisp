;;;; -*- Mode: LISP; Syntax: Ansi-Common-Lisp; Base: 10; Package: CL-POSTGRES; -*-
(in-package :cl-postgres)

;; For more information about the PostgreSQL scocket protocol, see
;; http://www.postgresql.org/docs/current/interactive/protocol.html

;; Scram Functions following the specifications here:
;; RFC 5802 https://tools.ietf.org/html/rfc5802
;; RFC 7677 https://tools.ietf.org/html/rfc7677


(defun char-mapped-to-nothing-p (chr)
  "Returns t if the character should be mapped to nothing per RFC 3454 Table B.1 and RFC 4013"
;  (gethash ch hsh)
  (when (not (or (characterp chr) (integerp chr)))
    (bad-char-error "Passing unknown type data to char-mapped-to-nothing-p" :value chr))
  (let ((chr-code-point (if (integerp chr) (coerce chr 'fixnum) (char-code chr))))
    (declare (optimize speed)
             (integer chr-code-point))
    (if (or (member chr-code-point '(#x00AD #x1806 #x200B #x2060 #xFEFF #x034F #x180B #x180C #x180D #x200C #x200D))
                    (and (>= chr-code-point #xFE00) (<= chr-code-point #xFE0F)))
        t
        nil)))

(defun char-mapped-to-space-p (chr)
  "If character is mapped to space per RFC 3454 Table C.1.2 and RFC 4013, then return t, else nil"
  (when (not (or (characterp chr) (integerp chr)))
    (bad-char-error "Passing unknown type data to char-mapped-to-space-p" :value chr))
  (let ((chr-code-point (if (integerp chr) (coerce chr 'fixnum) (char-code chr))))
    (declare (optimize speed)
             (integer chr-code-point))
    (if (or (member chr-code-point '(#x00A0 #x1680  #x202F #x205F #x3000))
            (and (>= chr-code-point #x2000) (<= chr-code-point #x200B)))
      t
      nil)))


(defun string-mapped-to-nothing (str)
  "Reads a string and removes any character that should be mapped to nothing per RFC 3454 and RFC 4013."
  (let ((s1 (coerce str 'simple-vector))
        (lst nil))
    (loop for x across s1 counting x into y do
         (cond ((char-mapped-to-nothing-p x))
               ((characterp x)
                (push x lst))
               (t (return-from string-mapped-to-nothing))))
    (setf lst (nreverse lst))
    (format nil "窿祗舂┅ㄤ彐躅篝蜷铉磲痧邃麸箴徙篝颟⒁遽潴篝蜷铉犷泔铞弪趔犷汨狎徙翦麒殂箬秕熹忮磲痧邃麸箴徙痱移炒荡犷移窗背麸箴徙瀹戾è蟊ㄣ镥蜚篝箝眇戾鲥泗矧┅祜镳骘徙蝻篌蟊泔躅糸铉轭麸滹麒孱ㄣ栳颦磲痧邃麸箴徙瀛箦翩ㄡ蝈蟊ō暴＼羽徙濠┅ㄣ镥蜚蟊篝蜷铉┅ㄤ彐躅筢箪痱屦铒蝽犰辁篝镳糸镱犰ㄦ矧侯骐悌⒂汜铙篝蜷铉涉犷汨狎徙翦箬秕熹忮磲痧邃麸铒翳轭绗轸屐轫轭狒弩翳狒汨狎徙翦虍涉犷汨狎徙翦轶铒痱轭翎忪狍汩楝轸蝈趱蝾铋飚涉弼弪汨狎徙翦蝈磲轭轭徭翦屐轫轭狒轱铙轶痱轭翎忪狍汩楝轸蝈趱蝾翳痱轭翎忪瀛狍汩篝蜷铉麒孱篝蜷铉痱轭翎忪瀛狍汩榄篝颟蝈趱蝾骝镯筢箪痱屦铒蝽犰辁篝颟箦翩篝篝蜷铉磲痧邃麸铒翳轭篝颟箦翩篝篝蜷铉磲痧邃麸箴徙篝颟箦翩篝踽钡侯矧磲扉篝骘蝽┅ㄤ彐沆狍溟筱蝈翦祜玑蜷翳憝珧秕īè洪铋翎蜱吼候遽溴珧秕瓠瘀犰洪铋翎蜱厚候遽溴珧秕瓠聆犰ㄧ洪铋翎蜱虹候遽溴珧秕瓠琏犰┅换泔铞弪糸铉骝镯轭翦珏蝮麸镢翦鲥泗矧ㄤ彐躅镢翦趔麸轭翦珏镢翦舡鲥脲篝狎癌孱ㄢ殓孱溟犷舂瞽忾趔ㄤ邈灬蝈豉疱箝眇戾狎蜥躅箝珙邃怡翦俯í┅镢翦舡鲥悌镳糸黹箴邋畅箴徙癌筢驽豉暴ㄤ邂蹒癌┅戾è孱矧孱戾铉翳镢翦舡鲥悌┅眭祠轲戾鲠祯瀛忾钿瞽忾趔瞽怡翦螬戾è箝ō孱篝狎舂┅ㄩ瞽忾趔鲠祯弩瞽忾趔黹ㄣ彘扉铉瞽忾趔俯箝濠鲠祯弩í箝濠箝濠┅戾è篚ㄩ忾绛孱溟犷祜镳鏖翳篚骘骝镯ō孱瞽怡翦螬忮祜孱滹箦翩篚ǐㄡ箬篚俯ㄡ蝈镢翦舡鲥椹┅骈钺祆蝈趱蝾篚愆祜镳骘骝镯篝狎忮祜ǐ篝狎瞽怡翦螬骘骝镯怡篚ㄡ箬ㄡ蝈镢翦舡鲥椹戛┅┅熹ㄢ翦瞽忾趔癌篚愆┅┅ㄤ彐躅轭翦珏颦麸镢翦趔ㄢ殓铛脲瞽忾趔ㄢ殓孱溟犷舂ㄤ邈灬蝈镳糸黹箴邋畅箴徙癌筢驽豉暴ㄤ邂蹒癌┅戾舄è瞽忾趔矧瞽忾趔ㄩ铘彗弪戾铉翳忾珙蹴┅ㄢ殓铛熹ㄢ翦瞽忾趔癌忾珙蹴┅瞽怡翦ㄣ彘扉铉瞽忾趔俯镢翦舡鲥磲脲狎蜥瞽怡翦哄戾礤铘豉疱Ж躅箝珙邃怡翦俯┅ㄤ邈灬蝈豉疱箝眇戾狎蜥躅箝珙邃怡翦俯í┅镢翦舡鲥悌ㄩ忾绛孱溟犷祜镳骘骝镯ū瞽怡翦螬滹黝麸骘轭溴骝镯滹箦翩ㄡ蝈镢翦舡鲥轭溴熹ㄢ翦í俯忾珙蹴┅骈钺祆蝈趱蝾镢翦舡鲥悌祜镳骘骝镯忮祜瞽怡翦骘怡翦骝镯怡滹箦翩ㄡ蝈镢翦舡鲥椹熹ㄢ翦怡翦忾珙蹴┅骈钺祆蝈趱蝾镢翦舡鲥悌┅┅ㄤ彐躅磲忮轭翦珏蜷翳轭绌ㄥ豉疱汜箦翳轭ㄩ铘彗弪翳轭绌è箝眇戾狎蜥躅箝珙邃怡翦俯í┅镢翦趔麸轭翦珏翳轭绌┅ㄤ彐躅怡翦狎蜥麸桢篝蜷铉鲥泗矧脲篝狎癌孱ㄥ戾礤铘豉疱р狍瀛汨狎┅⒁弭躜篝蜷铉泔铘衢铋铉翳桢徜邈轫犰蝈痱弩孱翎糸镱镦翳篚怏羼蹂钽镦峙迷弦忮赭邋釉烈犷盼漠盘磐盼原再信泔铘蝻祗翳屐屙孱舡豉疱镦翳蝈趱蝾邃篝蜷铉ㄤ邈灬蝈豉疱鲥泗矧躅箝珙邃怡翦俯鲥泗矧豉疱骈铛篝狎舂豉疱矧铛祆骈铛愆孱洎镳糸黹箴邋畅筢驽豉暴┅戾舄è孱矧孱戾铉翳鲥泗矧┅戾铉翳ō孱篝狎舂ㄨ屮溟玳趔．ㄣ镥蜚氨渤吹斗腹徕沅彐箝眇戾忉箦篝蜷铉┅祜镳鏖翳篝蜷铉ㄥ汜箦屐屙孱舡豉疱换箫翳狒翳泔眇殪弪镳糸黹狒轱汜牾眇轭ㄢ狍瀛汨狎磲脲篝蜷铉í戾铉翳博哄戾礤铘豉疱р狍瀛汨狎┅ㄣ栳蜥泗弪磲脲篝蜷铉í戾铉翳博哄戾礤铘豉疱с栳蜥泗弪┅骘骝镯篝狎忮祜孱骘骝镯忮祜í戾铉翳博怡滹戾è怡翦ㄡ蝈鲥泗矧椹┅ㄤ邈灬蝈镳糸黹筢驽豉癌┅箦翩ㄡ蝈篝蜷铉戛ㄡ蝈桢溟玳趔熹ㄢ翦穿怡翦┅ㄡ蝈篝蜷铉ū戛ㄡ蝈桢溟玳趔熹ㄢ翦癌怡翦┅┅骈钺祆蝈趱蝾篝蜷铉┅┅ㄤ彐躅桢篝蜷铉麸怡翦狎蜥篝蜷铉脲篝狎癌ㄥ钿铋飑⑿狎箦篚怏趄轭镦釉疑吻溴扉黹翦怡釉烈犷盼镦桢徜邈轫犰溟玳趔轭麸怡翦狎蜥ㄤ邈灬蝈豉疱篝蜷铉篝蜷铉┅戾舄è孱矧孱戾铉翳篝蜷铉┅戾铉翳ǒō孱篝狎舂博脲磲脲狎蜥戾铉翳哄戾礤铘豉疱Ж躅箝珙邃怡翦俯┅ㄤ邈灬蝈豉疱箝眇戾狎蜥躅箝珙邃怡翦俯í┅脲┅ㄦ戾è汨狎麸溟玳ㄣ栳颟矧痫箝糸镱汨狎氨渤吹斗腹徕沅彐呼弩＇汨狎羼踽飑ㄥ蝌矧ч蝻钽灬洵弪蝻烘矧磲舡泔铘蝻轶铒桢溟玳簪烘矧磲舡狎珲礤铘扉篝汨狎┅┅祜镳骘骝镯骘骝镯篝狎忮祜孱怡滹箦翩ㄡ蝈脲椹ǐíㄣ栳颦麸溟玳ㄣ栳篝蜷铉戛倍ㄣ栳颦麸溟玳ㄣ栳篝蜷铉ū戛┅┅骈钺祆蝈趱蝾脲┅┅ㄤ彐躅狍汩榄篝蜷铉麸怡翦狎蜥篝蜷铉脲篝狎癌孱洎⒚镱鲥螋釉疑吻麸ㄖ琶韵ㄕ斡汕闻沫沦耘俯┊婶轶犷弪蝻殒釉疑吻泔铘衢铙犷汨狎徙翦麒矬萌烈孟呐轶珧遽翦翳犷驳诞ㄤ邈灬蝈豉疱篝蜷铉篝蜷铉豉疱骈铛篝狎舂豉疱矧铛祆骈铛愆孱洎镳糸黹箴邋畅筢驽豉暴┅戾舄è戾铉翳戾铉翳篝蜷铉┅鲥磲脲狎蜥戾铉翳哄戾礤铘豉疱Ж躅箝珙邃怡翦俯┅ㄥ钿矧孱戾铉翳┅祜镳骘骝镯篝狎忮祜孱滹戾è怡翦ㄣ栳颦泔溴ㄣ栳篝蜷铉椹┅躅戾篌怡翦驳订ㄥ蝌矧ч蝻钽灬洵弪蝻烘矧磲舡泔铘蝻轶铒犷劣蒙汨狎徙翦颌烘矧磲舡狎珲礤铘扉篝ㄣ栳篝蜷铉椹┅箦翩ㄡ蝈鲥椹怡翦┅骈钺祆蝈趱蝾鲥悌┅ㄤ邈灬轫铒糸铎轭怡翦狎蜥麸桢篝蜷铉桢篝蜷铉麸怡翦狎蜥狍汩榄篝蜷铉麸怡翦狎蜥┅ㄤ彐珏铄蜷溟珏篝戾铉翳ㄤ殓弩舂ê滹沲礤铘狒轱⒁弭躜翳铛礅弪镦怡翦轭溟珏篝珏铄蜥翦怡纳桥釉┅ㄤ彐溟珏篝箬岵刀轰殓弩舡戾铉翳巢衡祜汶戾铉翳洞ㄤ彐沆狍桧徙磲悌è轭铄颦溟珏篝候遽溴轭铄颦溟珏篝洪铋翎蜱洪铑弪溟珏篝秕翦颦溟珏篝候遽溴秕翦颦溟珏篝洪铋翎蜱猴豸弪溟珏篝┅ㄤ彐礤翳镤痱轭舡镡赍泗è磲桧徙篝蝈犴痱轭舡躅蝈徜徕戾镡赍泗磲篝蝈犴呼疱铋洪溴铘轸舂ㄦ矧磲篝蝈犴⑷土猫俩豉疱镦ㄩ铑弪溟珏篝磲悌┅┅ㄤ彐躅磲脲桧徙脲溟珏篝钺礤磲脲轭篝犷沐ц磲弘妁脲洪铑弪溟珏篝磲脲溟珏篝溟珏篝钺礤猴豸弪溟珏篝磲脲溟珏篝溟珏篝钺礤┅ㄤ彐躅躔溽翦桧徙ㄨ磲箦聃孱沐脲篝狎癌ㄥ钿戾铉翳箦聃孱沐┅ㄤ邈灬蝈豉疱箝眇戾狎蜥躅箝珙邃怡翦俯í┅箦聃孱沐┅躔溽翦溟珏篝ㄩ铑弪溟珏篝桧徙箦聃孱沐后翎螋篝狎哄钿孱洎桧徙ㄤ彐躅桧徙溟珏篝ㄨ磲脲怩骀弪ㄢ蹑驽颦篝狎癌戾舄èㄣ镳溟珏篝ㄩ铑弪溟珏篝桧徙┅ㄩ铑弪栳箬痱镤蹉瀛溟珏篝轰殓弩怩骀弪轰殓弩舡篝狎怩骀弪篝狎舂┅ㄣ镳溟珏篝秕翦颦溟珏篝桧徙躔溽翦溟珏篝轭铄颦栳箬轰殓弩怩骀弪轰殓弩舡篝狎怩骀弪篝狎舂痱镤蹉瀛溟珏篝轰殓弩怩骀弪轰殓弩舡篝狎怩骀弪篝狎舂┅ㄤ彐躅踱巢蝈姣忮鲥泗矧镦骟弭ㄤ邈灬蝈豉疱箝眇戾镢翦舡鲥泗矧鲥泗矧豉疱轭溴镦骟弭┅＋ㄡ钿沣轵镱沆徜狍箦礅禊付洞ē踱巢蝈姣忮鲥泗矧镦骟弭＋ㄡ钿邈轵镱沆徜狍箦礅禊扉趑戾孱溟犷ㄦ骈恒轭扉铄鲥泗矧镦骟弭乎铙殓铄洵轭舂乎轭舫箔Ⅺ蹰铘巢唪è蹰铘巢唪è０┉踞蝌狴箦戽飧１┅蹰铘巢唪技泊è傍骀鞍技俯è揪俯傍骀鞍揪泊┗括蝈趱蝾癌蚧后殇瀛彐驽泗铋飑＋ㄡ钿筲沆忾绛孱溟犷筲簌蠛筢瓠蝈姝巢筲簌蠛鲥泗矧筢鲥泗矧镦骟弭＋ㄡ钿筲沆轵镱沆徜狍箦礅禊矧付付洞┅篦狃巢筲簌蠛筢瓠蝈姝巢筲簌蠛鲥泗矧筢鲥泗矧镦骟弭┅－矧ㄡ钿沣轵镱沆徜狍箦礅禊付洞ㄡ钿邈轵镱沆徜狍箦礅禊扉趑戾孱溟犷ㄡ钿筲沆忾绛孱溟犷ㄡ钿筲沆轵镱沆徜狍箦礅禊矧付付洞┅ㄤ疴踱倍蝈姣忮鲥泗矧镦骟弭ㄢ翦倍倍踱倍蝈姣忮鲥泗矧ǐ镦骟弭博┅ㄤ彐礤翳镤溴蜷鲥脲è脘疴脘娌疳篌痂蜥箦筢祠轸弪狒轱瞽泔躅脲戾铉翳疴脘娌溴蜷鲥脲脘姝溟珏篝脘姗疳篌痂蜥箦筢祠轸弪狒轱瞽泔躅脲戾铉翳┅ㄤ彐躅疴脘娌栳箬疳篌黠蜾疳篌黠蜾脲筢祠磲脲蜥钿镯筢祠┅ㄤ殓弩箬岵刀ㄩ翦蜥糸镱卑鞍┅⑶轹孱辛佑紫夷狍怡翦鲥泗矧恿淘狍怡翦鲥泗矧ㄍ了怒伊文贤恿淘轶汜祆邃麸珏铄蜥翦蜥钿镯筢祠殒铒铄轶痱秭殇邃┈溟珏篝骢钽糸镱ㄓ攘驳怡溴驷蹯舂犷铛礅弪镦轸弪狒轱铙ū鞍癌蝈趱蝾翳新四撇溴蜷鲥栳箬镦翳疳篌黠蜾ㄢ翦鲥泗矧狍翳骈蝮鲠祯瀣犷翳恿淘ㄢ翦鲥泗矧狍翳箦泔钿鲠祯瀹鲠祯弩疴脘娌溴蜷鲥脲溟珏篝疳篌黠蜾筢祠轸弪狒轱铙ㄤ殓弩舡戾铉翳溟珏篝┅筢祠┅ㄤ彐躅疴脘娌溴蜷鲥脲ㄤ殓弩疳篌痂蜥箦筢祠轸弪狒轱瞽泔躅脲戾铉翳ㄣ桢汶豉疱轸弪狒轱瞽泔躅ㄩ铘彗弪┅ㄣ桢汶豉疱脲戾铉翳ㄩ铘彗弪┅祜镳鏖翳泔躅鏖翳桧徙磲脲桧徙疳篌痂蜥箦溟珏篝鏖翳桧徙戾铉翳ㄤ殓弩舡戾铉翳溟珏篝鏖翳脲磲脲狎蜥脲戾铉翳哄戾礤铘豉疱Ж躅箝珙邃怡翦俯洪铋糸犰屐屙孱癌鏖翳脲痫箝糸镱鏖翳泔躅舡怩骀弪磲脲狎蜥哄戾礤铘豉疱Ж躅箝珙邃怡翦俯鏖翳桧徙秕磲脲狎蜥桧徙戾铉翳哄戾礤铘豉疱Ж躅箝珙邃怡翦俯麒殪痨躞脲戾铉翳滹戾è箝黹桧徙戾铉翳脲戾铉翳┅蝈轭轸獒扉瀛轭篝犷沐桧徙弘妁疳篌痂蜥箦躔溽翦桧徙桧徙筢祠箦翩踱巢蝈姣忮泔躅舡怩骀弪癌泔躅舂躔溽翦桧徙桧徙泔躅舡怩骀弪ㄨ磲悱溟珏篝桧徙衡蹑驽桧徙秕舂矧忪镢箝桧徙秕脲脲痫箝糸镱脲脲痫箝糸镱祜镳骘骝镯忮祜轸弪狒轱瞽泔躅滹蝈轭轸獒扉瀛轭篝犷沐桧徙弘妁疳篌痂蜥箦躔溽翦桧徙桧徙桧徙秕舂ㄨ磲悱溟珏篝桧徙衡蹑驽桧徙秕舂矧忪镢箝桧徙秕脲脲痫箝糸镱脲脲痫箝糸镱骈钺祆ㄤ邈脲戾铉翳箝濠ㄩ钽脲痫箝糸镱箝濠ㄩ钽泔躅舂┅骈钺祆蝈趱蝾脲┅ㄤ彐珏铄蜷溟珏篝箦聃孱沐ㄤ殓弩舡箴邈箦聃孱沐蝈篝狎珞脲篝狎孱溟珏篝溟珏篝篝狎舂ê滹沲礤铘狒轱⒁弭躜翳溟珏篝镦翳篚怏羼蹂钽镦优颜盼门箴邈殒殄怡釉烈犷盼躞轭翳犰顼蜷翳纳桥釉瘟团骑猛彰犷勇锰优颜盼门汜忮犷鲥泗矧鏖翳犷屐屙孱舡豉疱镦ㄕ斡汕闻沫沦耘俯骘雉桢轫痨屙孱翎糸镱蟋优颜盼门眭篝忮ㄓ赏刑怒烈伊ㄕ斡汕闻沫沦耘俯í┅涉纳桥釉轶痱秭殇邃翳溟珏篝鏖祆忮痨徙邃轭麸纳桥釉篝狎糸铉狒纳桥釉釉烈援纳桥釉眭篝忮ㄓ赏刑怒烈伊ㄕ斡汕闻沫沦耘俯í┅令弪蝻鏖祆忮箝珙犰邃殒翳弪轶轭篚骀殂殄铘蝻镯轭纳桥釉┅ㄤ彐轭瀛泔眇殪弪磲泸溟珏篝箦聃孱沐é麒镬骘蝽孱鲩蝻铐孱孱钺礤箦聃孱沐蝈篝脲螬ㄤ邈灬蝈ㄩ珙矧孱雯磲忮屮疳钿镱瀛箬雉汜祆骘蝽т殓弩舡箦聃孱沐钺礤箦聃孱沐脲螬ㄤ彐礤翳镤溟珏篝箦聃孱沐è溟珏篝钺礤簌礅镬箦聃孱沐蝈篝膑狎珞ㄡ痧禊＇溟珏篝箦聃孱沐磲脲溟珏篝溟珏篝钺礤箦聃孱沐膑狎珞┅ㄤ彐礤翳镤溟珏篝箦聃孱沐篝狒箦聃孱沐脲篝狎癌孱溟珏篝ㄤ殓弩舡篝狎癌＋矧沩筲沆祜汜祆ㄤ邈灬蝈豉疱鲥泗矧躅箝珙邃怡翦俯箦聃孱沐豉疱轭溴篝狎舂换蝈箴邈翳骈祆痫轭翦戾è孱矧孱戾铉翳箦聃孱沐┅┅ㄤ邈灬蝈豉疱轭溴孱洎ǎ沩扉箴汉鏖翳狎蜥溽翎＋筲沆筲脲蝾屐瑚轸璀狎蜥溽翎è溽翎箦聃孱沐蝈犰篝狎篝狎舂蝈犰孱孱洎ㄤ邈灬蝈ㄩ珙矧蝈犰孱洎躔溽翦溟珏篝篝狒溽翎后翎螋蝈犰篝狎哄钿ǐ蝈犰篝狎ō孱篝狎舂┅┅－矧沩筲沆戾è蝈犰孱矧孱戾铉翳箦聃孱沐┅┅躔溽翦溟珏篝篝狒箦聃孱沐后翎螋篝狎哄钿矧蝈犰孱戾铉翳箦聃孱沐┅┅痱镤蹉瀛溟珏篝篝狒轰殓弩溟珏篝轰殓弩舡篝狎溟珏篝篝狎舂换澡弩骘躜骢钽糸镱蝈痱弩孱翳泔眄镱轭翦蜴徙骘溟珏篝轭换雉桢泸痿麸镬腴趔ㄏ疱钣犹嘛翎瞵轩翳镱弭惝┊亠镡翎轭换箫礤篝狒镡赍泗骘疳螋殂蹯狎溟珏篝秕躔溽翦轸鏖翳箫礤换溽翎犷翳孱秕珏翳徙趱犰溟珏篝旗屮殁殪轸轶翳钺礤换镦翳玑礤鏖翳翳弩骢钽糸镱螽ㄤ彐躅磲脲溟珏篝ㄤ殓弩舡钺礤蝈篝脲脲犰祜鳝雉桢颦脲螬⒁弭躜溟珏篝镡赍泗麒殂躞弩翳犰顼蜷翳纳桥釉瘟团豉疱汜箦溟珏篝钺礤簌礅镬戾è钺礤磲篌徵瀛簌礅镬溟珏篝钺礤┅ㄩㄤ殓弩麴钺礤ㄡ痧禊翳骢钽糸镱ㄧ弭钺礤Д磲脲溟珏篝┅脲螬ㄥ蝌矧躅篚痧矧翦洵溟珏篝侯犴溟珏篝钺礤┅┅ㄥ蝌矧豉疱弪蝻轰狒蹴溟珏篝钺礤哄疱泗邃豉疱簌礅镬┅┅